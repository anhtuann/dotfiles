# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.1
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"

source=("http://anhtuann.com/$pkgname-$pkgver-custom.tar.gz"
        "config.h")

sha256sums=('a43753c0d051b9e2e65b8420a68d4b2758fa6b93c22a21016bbd3287b4c45a64'
            '08d00d549adc5362fddb9c904d4a58f7b4b7ce96efd70c128eb9e5da75313572')

prepare() {
  cp config.h $srcdir/$pkgname-$pkgver-custom/config.h
  cd $srcdir/$pkgname-$pkgver-custom
  # skip terminfo which conflicts with nsurses
  sed -i '/tic /d' Makefile
}

build() {
  cd $srcdir/$pkgname-$pkgver-custom
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver-custom
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
